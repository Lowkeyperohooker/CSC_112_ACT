LANGUAGE SPECIFICATION
TOKENS (LEXICAL UNITS)

KEYWORDS: ilimbag : Print command 
numero : Integer type declaration 
desimal : Float type declaration 
letra : Character type declaration 
sulat : String type declaration

OPERATORS & ASSIGNMENT:
       = : ASSIGN
       + : PLUS
       - : MINUS
       * : MULTIPLY
       / : DIVIDE
       ++ : INCREMENT
       -- : DECREMENT
       += : PLUS_ASSIGN 
       -= : MINUS_ASSIGN
       *= : MULTIPLY_ASSIGN
       /= : DIVIDE_ASSIGN

SEPARATORS & GROUPING:
       , : COMMA
       ( : LPAREN
       ) : RPAREN
       \n : NEWLINE

LITERALS & IDENTIFIERS:
       INTEGER : Sequence of digits (e.g., 10, 42)
       FLOAT : Decimal numbers (e.g., 3.14, 0.5)
       STRING_LITERAL: Double-quoted text (e.g., "Hello World")
       CHARACTER : Single-quoted character (e.g., 'A')
       IDENTIFIER : Variable names (starts with letter/underscore)

CONTEXT-FREE GRAMMAR (BNF NOTATION)

<non-terminal> ::= definition

<program> ::= <program> <line>
            | ε

<line> ::= <statement> NEWLINE
         | NEWLINE

<statement> ::= <declaration>
              | <assignment>
              | <print_stmt>
              | <expr>

<declaration> ::= numero IDENTIFIER
                | numero IDENTIFIER = <expr>
                | desimal IDENTIFIER
                | desimal IDENTIFIER = <expr>
                | letra IDENTIFIER
                | letra IDENTIFIER = <char_val>
                | sulat IDENTIFIER
                | sulat IDENTIFIER = <string_val>

<assignment> ::= IDENTIFIER = <expr>
               | IDENTIFIER += <expr>
               | IDENTIFIER -= <expr>
               | IDENTIFIER *= <expr>
               | IDENTIFIER /= <expr>
               | IDENTIFIER = <char_val>
               | IDENTIFIER = <string_val>
               | IDENTIFIER ++
               | IDENTIFIER --
               | ++ IDENTIFIER
               | -- IDENTIFIER

<print_stmt> ::= ilimbag <print_items>
               | ilimbag

<print_items> ::= <print_item>
                | <print_items> , <print_item>

<print_item> ::= STRING_LITERAL
               | CHARACTER
               | <expr>

<expr> ::= <term>
         | <expr> + <term>
         | <expr> - <term>

<term> ::= <factor>
         | <term> * <factor>
         | <term> / <factor>

<factor> ::= INTEGER
           | FLOAT
           | IDENTIFIER
           | - <factor>
           | + <factor>
           | ( <expr> )

<string_val> ::= STRING_LITERAL

<char_val> ::= CHARACTER

<IDENTIFIER> ::= <letter>
               | <IDENTIFIER> <letter>
               | <IDENTIFIER> <digit>
               | <IDENTIFIER> _

<INTEGER> ::= <digit>
            | <INTEGER> <digit>

<FLOAT> ::= <INTEGER> . <INTEGER>

<STRING_LITERAL> ::= " <string_content> "

<string_content> ::= <any_char>
                   | <string_content> <any_char>
                   | ε

<CHARACTER> ::= ' <any_char> '

<letter> ::= a | b | c | d | e | f | g | h | i | j | k | l | m 
           | n | o | p | q | r | s | t | u | v | w | x | y | z
           | A | B | C | D | E | F | G | H | I | J | K | L | M 
           | N | O | P | Q | R | S | T | U | V | W | X | Y | Z

<digit> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9

<any_char> ::= <letter> 
             | <digit> 
             | <symbol>
             | <space>

<symbol> ::= ! | @ | # | $ | % | ^ | & | * | ( | ) | - | _ | + | =
           | { | } | [ | ] | | | \ | : | ; | < | > | , | . | ? | /

<space> ::= " "
