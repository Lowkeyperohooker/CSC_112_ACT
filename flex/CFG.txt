PROGRAM → STATEMENT_LIST

STATEMENT_LIST → STATEMENT
               | STATEMENT_LIST STATEMENT

STATEMENT → DECLARATION NEWLINE
          | ASSIGNMENT NEWLINE
          | PRINT_STMT NEWLINE
          | UNARY_STMT NEWLINE
          | COMPOUND_ASSIGN_STMT NEWLINE
          | NEWLINE  (empty statement)

DECLARATION → NUMERO IDENTIFIER_LIST
            | SULAT IDENTIFIER_LIST

IDENTIFIER_LIST → IDENTIFIER
                | IDENTIFIER = EXPRESSION
                | IDENTIFIER_LIST , IDENTIFIER
                | IDENTIFIER_LIST , IDENTIFIER = EXPRESSION

ASSIGNMENT → IDENTIFIER = EXPRESSION

COMPOUND_ASSIGN_STMT → IDENTIFIER COMPOUND_OP EXPRESSION

COMPOUND_OP → += | -= | *= | /=

UNARY_STMT → INCREMENT IDENTIFIER
           | DECREMENT IDENTIFIER
           | IDENTIFIER INCREMENT
           | IDENTIFIER DECREMENT

PRINT_STMT → ILIMBAG PRINT_ARGS

PRINT_ARGS → EXPRESSION
           | STRING_LITERAL
           | STRING_LITERAL , EXPRESSION
           | PRINT_ARGS , EXPRESSION

EXPRESSION → TERM
           | EXPRESSION + TERM
           | EXPRESSION - TERM

TERM → FACTOR
      | TERM * FACTOR
      | TERM / FACTOR

FACTOR → NUMBER
       | CHAR_LITERAL
       | IDENTIFIER
       | ( EXPRESSION )
       | - FACTOR
       | + FACTOR
       | INCREMENT IDENTIFIER
       | DECREMENT IDENTIFIER
       | IDENTIFIER INCREMENT
       | IDENTIFIER DECREMENT
